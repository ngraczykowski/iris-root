@Library('pipeline-shared-library@master') _

def version = ""

@NonCPS
def isPreviousBuildFailed() {
  def previousBuild = currentBuild.getPreviousBuild()
  return previousBuild != null && previousBuild.result != 'SUCCESS'
}

pipeline {
  agent {
    label 'ec2-agent'
  }
  options {
    gitLabConnection(env.GITLAB_CONNECTION_NAME)
    disableConcurrentBuilds()
    ansiColor('xterm')
  }
  stages {
    stage('Version') {
      steps {
        script {
          version = sh(returnStdout: true, script: './scripts/get-version.sh').trim()
        }

        updateBuildName displayName: "#${env.BUILD_NUMBER} v${version}"
      }
    }
    stage('Applications') {
      environment {
        ARTIFACTORY = credentials('artifactory-deployment-user')
      }
      steps {
        withGradle {
          withCredentials(
              [usernamePassword(
                  credentialsId: 'artifactory-user',
                  usernameVariable: 'S8_REPO_USER',
                  passwordVariable: 'S8_REPO_PASSWORD')]) {
            sh "./gradlew -Pversion=\"${version}\" clean build"

            withDockerRegistry(
                url: 'https://docker.repo.silenteight.com',
                credentialsId: 'artifactory-deployment-user') {
              script {
                if (env.BRANCH_NAME == 'master') {
                  sh """
                    ./gradlew \
                      -Dsilenteight.repoDeploymentUser="\$ARTIFACTORY_USR" \
                      -Dsilenteight.repoDeploymentPassword="\$ARTIFACTORY_PWD" \
                      -Djib.console=plain \
                      -Pversion="${version}" \
                      publish \
                    && \
                      jq --raw-output '.image' \$(find . -type f -name 'jib-image.json') >.jib-images
                  """
                }
              }
            }

            stash name: 'jib', includes: '.jib-images', allowEmpty: true
          }
        }
      }
      post {
        always {
          discoverGitReferenceBuild()
          junit allowEmptyResults: true, testResults: '**/build/test-results/test/TEST-*.xml'
          jacoco()

          recordIssues enabledForFailure: true, tools: [java(), javaDoc()]
          recordIssues enabledForFailure: true, tool: checkStyle(
              pattern: '**/build/reports/checkstyle/main.xml, **/build/reports/checkstyle/test.xml')
          recordIssues enabledForFailure: true, tool: taskScanner(
              includePattern: '**/*.java, **/*.groovy',
              ignoreCase: true,
              highTags: 'HACK',
              normalTags: 'TODO, FIXME',
              lowTags: 'XXX')
        }
      }
    }
    stage('Chart') {
      when {
        beforeAgent true
        branch 'master'
      }
      environment {
        ARTIFACTORY = credentials('artifactory-deployment-user')
      }
      steps {
        sh """
          helm package \
            --dependency-update \
            --app-version "${version}" \
            --version "${version}" \
            --destination build/charts \
            charts/sear

          helm push-artifactory \
            --skip-reindex \
            --username "\$ARTIFACTORY_USR" \
            --password "\$ARTIFACTORY_PSW" \
            build/charts/sear-"${version}".tgz \
            https://repo.silenteight.com/artifactory/helm
        """
      }
    }
    stage('ECR') {
      agent {
        label 'aws-worker'
      }
      when {
        beforeAgent true
        branch 'master'
      }
      steps {
        unstash name: 'jib'
        // Copy images from `.jib-images` file to ECR.
        sh '''
          # Login to ECR
          aws ecr get-login-password --region us-east-1 | \
            skopeo login --username AWS --password-stdin 278875966478.dkr.ecr.us-east-1.amazonaws.com

          while read line; do \
            aws ecr --region us-east-1 \
              create-repository --encryption-configuration encryptionType=KMS \
                --repository-name  $(echo $line | grep  -Po '(?<=\\/).*(?=\\:)') || true; \
            skopeo copy docker://$line \
              docker://278875966478.dkr.ecr.us-east-1.amazonaws.com/$(echo $line | grep -Po '(?<=\\/).*'); \
          done < '.jib-images'
        '''
      }
    }
    stage('Last known good build') {
      when {
        beforeAgent true
        branch 'master'
      }
      steps {
        sh 'git branch --force last-known-good-build'
        gitPush branch: 'last-known-good-build', force: true
      }
    }
  }
  post {
    success {
      script {
        if (isPreviousBuildFailed()) {
          slackSend color: 'good', channel: 'C03D4GD7RHP', attachments: [
              [
                  color      : 'good',
                  author_name: ':building_construction: Iris Build',
                  author_link: env.JOB_DISPLAY_URL,
                  title      : "${currentBuild.fullProjectName} build is fixed with <${env.RUN_DISPLAY_URL}|${currentBuild.displayName}>!",
              ]
          ]
        }
      }
    }
    failure {
      script {
        if (env.BRANCH_NAME == 'master') {
          notifyBuild color: 'danger', channel: 'C03D4GD7RHP',
              title: ':building_construction: Iris Build'
        }
      }
    }
  }
}
