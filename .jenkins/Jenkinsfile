@Library('pipeline-shared-library@master') _

def version = ""

pipeline {
  agent {
    label 'ec2-agent'
  }
  options {
    gitLabConnection(env.GITLAB_CONNECTION_NAME)
    disableConcurrentBuilds()
    ansiColor('xterm')
  }
  stages {
    stage('Version') {
      steps {
        script {
          version = sh(returnStdout: true, script: './scripts/get-version.sh').trim()
        }

        updateBuildName displayName: "#${env.BUILD_NUMBER} v${version}"
      }
    }
    stage('Applications') {
      steps {
        withGradle {
          withCredentials(
              [usernamePassword(
                  credentialsId: 'artifactory-user',
                  usernameVariable: 'S8_REPO_USER',
                  passwordVariable: 'S8_REPO_PASSWORD'),
               usernamePassword(
                   credentialsId: 'artifactory-deployment-user',
                   usernameVariable: 'repoDeploymentUser',
                   passwordVariable: 'repoDeploymentPassword')]) {
            sh """
                ./gradlew -Pversion="${version}" clean build

                if [[ \$BRANCH_NAME = 'master' ]]; then
                  ./gradlew \
                    -Dsilenteight.repoDeploymentUser="\$repoDeploymentUser" \
                    -Dsilenteight.repoDeploymentPassword="\$repoDeploymentPassword" \
                    -Djib.console=plain \
                    -Pversion="${version}" \
                    publish

                  jq --raw-output '.image' \$(find . -type f -name 'jib-image.json') >.jib-images
                fi
            """

            stash name: 'jib', includes: '.jib-images', allowEmpty: true
          }
        }
      }
      post {
        always {
          discoverGitReferenceBuild()
          junit allowEmptyResults: true, testResults: '**/build/test-results/test/TEST-*.xml'
          jacoco()

          recordIssues enabledForFailure: true, tools: [java(), javaDoc()]
          recordIssues enabledForFailure: true, tool: checkStyle(
              pattern: '**/build/reports/checkstyle/main.xml, **/build/reports/checkstyle/test.xml')
          recordIssues enabledForFailure: true, tool: taskScanner(
              includePattern: '**/*.java, **/*.groovy',
              ignoreCase: true,
              highTags: 'HACK',
              normalTags: 'TODO, FIXME',
              lowTags: 'XXX')
        }
      }
    }
    stage('Chart') {
      when {
        beforeAgent true
        branch 'master'
      }
      environment {
        ARTIFACTORY = credentials('artifactory-deployment-user')
      }
      steps {
        sh """
          helm package \
            --dependency-update \
            --app-version "${version}" \
            --version "${version}" \
            --destination build/charts \
            charts/sear

          helm push-artifactory \
            --skip-reindex \
            --username "\$ARTIFACTORY_USR" \
            --password "\$ARTIFACTORY_PSW" \
            build/charts/sear-"${version}".tgz \
            https://repo.silenteight.com/artifactory/helm
        """
      }
      post {
        success {
          echo 'Marking successful build'
          sh 'git branch --force last-known-good-build'
          gitPush branch: 'last-known-good-build'
        }
      }
    }
    stage('ECR') {
      agent {
        label 'aws-worker'
      }
      when {
        beforeAgent true
        branch 'master'
      }
      steps {
        unstash name: 'jib'

        // TODO(ahaczewski): Implement sync with ECR.
        // 1. Ensure repository in ECR exists, if not - create one
        // 2. Login to skopeo
        // 3. Copy/sync images from `.jib-images` to ECR.
      }
    }
  }
  post {
    failure {
      script {
        if (env.BRANCH_NAME == 'master') {
          notifyBuild color: 'danger', channel: 'C03D4GD7RHP',
              title: ':building_construction: Iris Build'
        }
      }
    }
  }
}
