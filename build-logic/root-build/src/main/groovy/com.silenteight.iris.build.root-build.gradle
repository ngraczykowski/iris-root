tasks.register('clean') {Task task ->
  task.group = 'build'
  task.description = 'Deletes the build directory in all subprojects.'

  subprojects {Project subproject ->
    task.dependsOn subproject.tasks.named('clean')
  }
}

tasks.register('build') {Task task ->
  task.group = 'build'
  task.description = 'Builds all subprojects.'

  subprojects {Project subproject ->
    task.dependsOn subproject.tasks.named('build')

    if (subproject.pluginManager.hasPlugin('org.cyclonedx.bom')) {
      def isSbom = System.properties.getOrDefault('sbom', '')
      if (isSbom) {
        task.dependsOn subproject.tasks.named('cyclonedxBom')
      }
    }
  }
}

tasks.register('publish') {Task task ->
  task.group = 'publishing'
  task.description = 'Publishes all publications produced by all subprojects.'

  subprojects {Project subproject ->
    if (subproject.pluginManager.hasPlugin('com.google.cloud.tools.jib'))
      task.dependsOn subproject.tasks.named('jib')

    if (subproject.pluginManager.hasPlugin('distribution')
        || subproject.pluginManager.hasPlugin('maven-publish')) {
      task.dependsOn subproject.tasks.named('publish')
    }
  }
}
