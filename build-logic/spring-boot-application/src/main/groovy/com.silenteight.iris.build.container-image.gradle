plugins {
  id 'com.silenteight.iris.build.java-base'

  id 'com.google.cloud.tools.jib'
}

jib {
  from {
    // NOTE(ahaczewski): Use debug version of the image for SNAPSHOT builds.
    def imageTag = "$version".endsWith("-SNAPSHOT") ? "debug-nonroot" : "nonroot"
    image = "gcr.io/distroless/java17-debian11:" + imageTag
  }
  to {
    def registry = System.properties.getOrDefault('jibRegistry', 'docker.repo.silenteight.com')
    def additionalTag = System.properties.getOrDefault('jibAdditionalTag', '')
    def isSnapshot = "$version".endsWith('-SNAPSHOT')
    def imageTag = isSnapshot ? "$version".minus('-SNAPSHOT') : "$version"
    // NOTE(ahaczewski): Have to put it into local array variable, as Jib's `tags` is
    // an immutable collection.
    def extraTags = []
    if (!isSnapshot)
      extraTags << "latest"
    if (additionalTag)
      extraTags << additionalTag

    image = "${registry}/iris/${project.name}:${imageTag}"
    tags = extraTags
  }
  container {
    appRoot = "/silenteight/app"
    jvmFlags = [
        "-XX:MaxRAMPercentage=75",
        "-Dfile.encoding=UTF-8",
        "-Dsun.jnu.encoding=UTF-8",
        "-Djava.net.preferIPv4Stack=true",
        "-Djava.io.tmpdir=/tmp",
    ]
    user = "nonroot"
    workingDirectory = "/silenteight"
  }
}
