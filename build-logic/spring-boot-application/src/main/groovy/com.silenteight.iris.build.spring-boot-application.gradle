plugins {
  id 'com.silenteight.iris.build.java-base'
  id 'com.silenteight.iris.build.report-aggregation'

  id 'maven-publish'

  id 'org.springframework.boot'
  id 'org.cyclonedx.bom'
  id 'com.gorylenko.gradle-git-properties'
}

cyclonedxBom {
  includeConfigs += ["runtimeClasspath"]
  projectType = "application"
  outputName = "${project.name}.bom"
}

gitProperties {
  keys = [
      "git.branch",
      "git.build.version",
      "git.closest.tag.commit.count",
      "git.closest.tag.name",
      "git.commit.id",
      "git.commit.id.describe",
      "git.commit.time",
      "git.dirty",
      "git.tags",
  ]
}

jar {
  preserveFileTimestamps false
  reproducibleFileOrder true

  // Generates Class-Path in .jar manifest
  doFirst {
    manifest {
      def runtimeClasspath = configurations.runtimeClasspath

      if (!runtimeClasspath.isEmpty()) {
        def classPathJars = runtimeClasspath.collect {it.getName()}.join(" ")

        attributes "Class-Path": classPathJars
      }
    }
  }
}

springBoot {
  buildInfo() {
    properties {
      time = null
    }
  }
}

// NOTE(dsniezek): Unfortunately it is needed to correct cooperation between maven-publish and
//  org.springframework.boot plugins. For more information, see:
//  https://docs.spring.io/spring-boot/docs/current/gradle-plugin/reference/htmlsingle/#publishing-your-application.maven-publish .
publishing {
  publications {
    bootJava(MavenPublication) {
      artifact bootJar
    }
  }
}
