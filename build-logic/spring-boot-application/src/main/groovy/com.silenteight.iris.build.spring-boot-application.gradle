plugins {
  id 'com.silenteight.iris.build.java-base'
  id 'com.silenteight.iris.build.report-aggregation'

  id 'maven-publish'

  id 'org.springframework.boot'
  id 'org.cyclonedx.bom'
  id 'com.gorylenko.gradle-git-properties'
}

dependencies {
  implementation libs.spring.boot.starter.web

  testImplementation libs.spring.test
  testImplementation libs.spring.boot.test
  testImplementation libs.spring.boot.test.autoconfigure
}

cyclonedxBom {
  includeConfigs += ["runtimeClasspath"]
  projectType = "application"
  outputName = "${project.name}.bom"
}

gitProperties {
  keys = [
      "git.branch",
      "git.build.version",
      "git.closest.tag.commit.count",
      "git.closest.tag.name",
      "git.commit.id",
      "git.commit.id.describe",
      "git.commit.time",
      "git.dirty",
      "git.tags",
  ]
}

jar {
  preserveFileTimestamps false
  reproducibleFileOrder true

  // Generates Class-Path in .jar manifest
  doFirst {
    manifest {
      def runtimeClasspath = configurations.runtimeClasspath

      if (!runtimeClasspath.isEmpty()) {
        def classPathJars = runtimeClasspath.collect {it.getName()}.join(" ")

        attributes "Class-Path": classPathJars
      }
    }
  }
}

springBoot {
  buildInfo() {
    properties {
      time = null
    }
  }
}
