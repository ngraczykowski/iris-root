import org.apache.tools.ant.filters.ReplaceTokens

plugins {
  id "distribution"
  id "maven-publish"
}

description = "Adjudication Engine Distribution"

configurations {
  dist
}

dependencies {
  dist project(path: ":dataprep-app", configuration: "bootArchives")
}

def runScript(String name) {
  copySpec {
    from(file("src/bash/run.template.sh")) {
      rename {
        name
      }
      filter(
          ReplaceTokens,
          tokens: [jarfile: "dataprep-app-${project.version}.jar".toString()],
          beginToken: "@@",
          endToken: "@@",
      )
      into("bin/")
    }
  }
}

distributions {
  main {
    distributionBaseName = "dataprep-dist"

    contents {
      from(configurations.dist) {
        into "lib/"
      }
      with runScript("dataprep")
    }
  }
}

tasks.named("distTar") {
  compression = Compression.BZIP2
  archiveExtension = "tar.bz2"
}

tasks.withType(Zip) {
  enabled = false
}

publishing {
  publications {
    distribution(MavenPublication) {
      artifact distTar
      artifactId "dataprep-dist"
    }
  }
}
