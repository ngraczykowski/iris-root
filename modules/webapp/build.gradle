plugins {
    id "idea"
    id "org.sonarqube" version "2.7"
    id "com.silenteight.gradle.wrapper" version "1.0.25"
    id "com.silenteight.gradle.release" version "1.0.9"
    id "com.silenteight.gradle.convention" version "1.11.0"
}

projectSetup {
    javaVersion = JavaVersion.VERSION_11
    lombokVersion = "1.18.8"
}

idea {
    module {
        [".gradle", "build"].each {
            excludeDirs += file("$projectDir/$it")
        }
    }
}

ext {
    gradleScriptDir = "${rootProject.projectDir}/gradle/scripts"
}

subprojects { Project project ->
    pluginManager.withPlugin("java") {
        project.apply from: "${gradleScriptDir}/dependencies.gradle"

        project.dependencies {
          implementation libraries.jaxb_api
          annotationProcessor libraries.spring_boot_configuration_processor
        }

        project.tasks.named(JavaPlugin.COMPILE_JAVA_TASK_NAME).configure { JavaCompile java ->
            def processResources = project.tasks.named(JavaPlugin.PROCESS_RESOURCES_TASK_NAME)

            java.dependsOn processResources
        }
    }
}

def uncommonSubprojects = subprojects - project(":sens-webapp-common-testing") - project(":sens-webapp-common")

configure(uncommonSubprojects) { Project subproject ->
  subproject.pluginManager.withPlugin("java") {
    subproject.dependencies {
      testImplementation project(":sens-webapp-common-testing")
    }
  }
}
