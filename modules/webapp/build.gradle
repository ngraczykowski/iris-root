plugins {
  id "idea"
  id "maven-publish"
  id "com.silenteight.gradle.convention" version "1.19.1"
  id "com.silenteight.gradle.release" version "1.4.0"
  id "com.silenteight.gradle.wrapper" version "1.1.0"
  id "com.silenteight.gradle.testreportspublisher" version "1.2.0"
  id "io.freefair.aspectj" version "5.3.0" apply false
  id "nebula.resolution-rules" version "7.6.0"
  id "org.sonarqube" version "2.8"
  // Spring boot plugin version should be same as Spring version in sep-dependencies
  id "org.springframework.boot" version "2.4.9" apply false
  id "com.google.cloud.tools.jib" version "3.1.4" apply false
}

projectSetup {
    javaVersion = JavaVersion.VERSION_11
    lombokVersion = "1.18.8"
}

idea {
    module {
        [".gradle", "build"].each {
            excludeDirs += file("$projectDir/$it")
        }
    }
}

ext {
    gradleScriptDir = "${rootProject.projectDir}/gradle/scripts"
}

apply from: "${gradleScriptDir}/resolutionRules.gradle"

subprojects { Project project ->
    pluginManager.withPlugin("java") {
        project.apply from: "${gradleScriptDir}/dependencies.gradle"

        project.pluginManager.apply "io.freefair.aspectj.post-compile-weaving"

        project.dependencies {
          implementation libraries.jaxb_api
        }

        project.aspectj {
          version = libraries.aspectjtools.version
        }

        project.tasks.named(JavaPlugin.COMPILE_JAVA_TASK_NAME).configure { JavaCompile java ->
            def processResources = project.tasks.named(JavaPlugin.PROCESS_RESOURCES_TASK_NAME)

            java.dependsOn processResources
        }
    }
}

tasks.named(BasePlugin.CLEAN_TASK_NAME).configure {
  dependsOn tasks.named("deleteWebappPlugins")
}

tasks.register("deleteWebappPlugins", Delete).configure {
  delete fileTree("${rootProject.projectDir}/plugin/webapp") {
    include "*.jar"
  }
}

configure(subprojects) { Project subproject ->
  subproject.pluginManager.withPlugin("java") {
    subproject.dependencies {
      testImplementation project(":sens-webapp-common-testing")
    }

    subproject.afterEvaluate {
      if (!subproject.pluginManager.hasPlugin("java-library"))
        generateClassPathInManifest(subproject)

      if (subproject.pluginManager.hasPlugin("com.google.cloud.tools.jib")) {
        subproject.jib {
          from {
            // NOTE(ahaczewski): Use debug version of the image for SNAPSHOT builds.
            def imageSuffix = "$version".endsWith("-SNAPSHOT") ? "-debug" : ""
            image = "gcr.io/distroless/java-debian10:11" + imageSuffix
          }
          to {
            image = "docker.repo.silenteight.com/webapp/webapp-app:${version}"
            if (!"$version".endsWith("-SNAPSHOT"))
              tags = ["latest"]
          }
          container {
            appRoot = "/silenteight/web-app"
            environment = [
                "SILENTEIGHT_HOME"                    : "/silenteight",
                "SENS_WEBAPP_DB_HOST"                 : "webapp-db",
                "SENS_WEBAPP_DB_PORT"                 : "5432",
                "SENS_WEBAPP_DB_NAME"                 : "sens",
                "SENS_WEBAPP_DB_USERNAME"             : "sens",
                "SENS_WEBAPP_DB_PASSWORD"             : "sens",
                "SENS_WEBAPP_RABBITMQ_HOST"           : "rabbitmq",
                "SENS_WEBAPP_RABBITMQ_PORT"           : "5672",
                "SENS_WEBAPP_RABBITMQ_USER"           : "dev",
                "SENS_WEBAPP_RABBITMQ_PASSWORD"       : "dev",
                "SPRING_PROFILES_INCLUDE"             : "linux,database,rabbitmq,messaging,debug",
                "SPRING_PROFILES_ACTIVE"              : "",
                "KEYCLOAK_CLIENT_ID"                  : "backend",
                "KEYCLOAK_ADAPTER_AUTH_SERVER_URL"    : "https://auth.silenteight.com/",
                "KEYCLOAK_ADAPTER_RESOURCE"           : "backend",
                "KEYCLOAK_ADAPTER_CREDENTIALS_SECRET" : "",
                "KEYCLOAK_ADAPTER_REALM"              : "sens-webapp",
            ]
            jvmFlags = [
                "-XX:MaxRAMPercentage=75",
                "-Dfile.encoding=UTF-8",
                "-Dsun.jnu.encoding=UTF-8",
                "-Djava.net.preferIPv4Stack=true",
                "-Djava.io.tmpdir=/tmp",
            ]
            ports = ["24410"]
            user = "nonroot"
            volumes = ["/silenteight/cert"]
            workingDirectory = "/silenteight"
          }
        }
      }
    }
  }
}

static def generateClassPathInManifest(Project project) {
  project.tasks.named(JavaPlugin.JAR_TASK_NAME).configure { Jar jar ->
    jar.doFirst {
      jar.manifest {
        def runtimeClasspath = project.configurations.runtimeClasspath

        if (!runtimeClasspath.isEmpty()) {
          def classPathJars = runtimeClasspath.collect { it.getName() }.join(" ")

          attributes "Class-Path": classPathJars
        }
      }
    }
  }
}
