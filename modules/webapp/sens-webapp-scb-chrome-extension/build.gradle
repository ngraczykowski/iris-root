import com.github.jengelman.gradle.plugins.shadow.transformers.PropertiesFileTransformer

plugins {
  id "com.silenteight.iris.build.java-library"
  id 'com.github.johnrengelman.shadow' version '5.0.0'
  id 'maven-publish'
}

description = "SENS WebApp SCB Chrome Extension"

configurations {
  pluginJar
}

// Use pluginJar configuration to define dependencies which should be included inside plugin JAR
dependencies {
  implementation libs.hibernate.validator
  implementation libs.spring.boot.starter.actuator
  implementation libs.spring.boot.starter.undertow
  implementation libs.spring.boot.starter.web

  implementation project(":sens-webapp-common")

  testImplementation libs.rest.assured
  testImplementation libs.rest.assured.spring.mock.mvc
}

shadowJar {
  archiveClassifier = 'plugin'

  mergeServiceFiles()

  append "META-INF/spring.handlers"
  append "META-INF/spring.schemas"

  configurations = [project.configurations.pluginJar]

  transform(PropertiesFileTransformer) {
    paths = ["META-INF/spring.factories"]
    mergeStrategy = "append"
  }
}

tasks.register("makePlugin", Copy).configure {
  from shadowJar
  into "$rootProject.projectDir/plugin/webapp"
}

tasks.register("deletePlugin", Delete).configure {
  delete fileTree("${rootProject.projectDir}/plugin/webapp") {
    include "$project.name-*.jar"
  }
}

tasks.named(JavaBasePlugin.BUILD_TASK_NAME).configure {
  dependsOn tasks.named("makePlugin")
}

tasks.named(BasePlugin.CLEAN_TASK_NAME).configure {
  dependsOn tasks.named("deletePlugin")
}

publishing {
  publications {
    shadow(MavenPublication) {publication ->
      from project.shadow.component(publication)
    }
  }
}
