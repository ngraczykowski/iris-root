import com.github.jengelman.gradle.plugins.shadow.transformers.PropertiesFileTransformer

plugins {
  id "java"
  id "maven-publish"
  id 'com.github.johnrengelman.shadow' version '5.0.0'
}

description = "SENS WebApp User Sync"

configurations {
  implementation.extendsFrom starterDatabase
  runtimeOnly.extendsFrom starterDatabaseRuntime
  pluginJar
  compile.extendsFrom pluginJar
}

dependencies {
  compile libraries.javax_validation_api

  compile libraries.spring_boot_autoconfigure

  implementation project(":sens-webapp-audit")
  implementation project(":sens-webapp-common")
  implementation project(":sens-webapp-logging")
  implementation project(":sens-webapp-user")

  implementation libraries.oracle

  implementation libraries.vavr
}

shadowJar {
  archiveClassifier = 'plugin'

  mergeServiceFiles()

  append "META-INF/spring.handlers"
  append "META-INF/spring.schemas"

  configurations = [project.configurations.pluginJar]

  transform(PropertiesFileTransformer) {
    paths = ["META-INF/spring.factories"]
    mergeStrategy = "append"
  }
}

tasks.register("makePlugin", Copy).configure {
  from shadowJar
  into "$rootProject.projectDir/plugin/webapp"
}

tasks.register("deletePlugin", Delete).configure {
  delete fileTree("${rootProject.projectDir}/plugin/webapp") {
    include "$project.name-*.jar"
  }
}

tasks.named(JavaBasePlugin.BUILD_TASK_NAME).configure {
  dependsOn tasks.named("makePlugin")
}

tasks.named(BasePlugin.CLEAN_TASK_NAME).configure {
  dependsOn tasks.named("deletePlugin")
}

publishing {
  publications {
    shadow(MavenPublication) {publication ->
      from project.shadow.component(publication)
    }
  }
}
