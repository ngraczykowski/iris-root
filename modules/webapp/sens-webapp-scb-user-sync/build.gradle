import com.github.jengelman.gradle.plugins.shadow.transformers.PropertiesFileTransformer

plugins {
  id "com.silenteight.iris.build.java-library"
  //TODO probably remove see https://silenteight.slack.com/archives/CQJC6T3ST/p1650969734375289
  id 'com.github.johnrengelman.shadow' version '7.1.2'
  id 'maven-publish'
}

description = "SENS WebApp User Sync"

configurations {
  pluginJar
}

//If a new dependency is required then it has be added here and in the sens-webapp-backend/build.gradle
//or it could be included in built shadow JAR by using pluginJar configuration.
dependencies {
  implementation libs.silenteight.sep.usermanagement.api
  implementation libs.jakarta.validation.api

  implementation libs.spring.boot.autoconfigure


  implementation libs.spring.security.web
  implementation libs.spring.security.config
  implementation libs.spring.security.cas

  implementation project(":sens-webapp-audit-api")
  implementation project(":sens-webapp-backend")
  implementation project(":sens-webapp-common")
  implementation project(":sens-webapp-logging")
  implementation project(":sens-webapp-user")

  implementation libs.springdoc.security

  implementation libs.oracle

  implementation doNotUseLibs.vavr

  testImplementation libs.rest.assured.spring.mock.mvc
}

shadowJar {
  archiveClassifier = 'plugin'

  mergeServiceFiles()

  append "META-INF/spring.handlers"
  append "META-INF/spring.schemas"

  configurations = [project.configurations.pluginJar]

  transform(PropertiesFileTransformer) {
    paths = ["META-INF/spring.factories"]
    mergeStrategy = "append"
  }
}

tasks.register("makePlugin", Copy).configure {
  from shadowJar
  into "$rootProject.projectDir/plugin/webapp"
}

tasks.register("deletePlugin", Delete).configure {
  delete fileTree("${rootProject.projectDir}/plugin/webapp") {
    include "$project.name-*.jar"
  }
}

tasks.named(JavaBasePlugin.BUILD_TASK_NAME).configure {
  dependsOn tasks.named("makePlugin")
}

tasks.named(BasePlugin.CLEAN_TASK_NAME).configure {
  dependsOn tasks.named("deletePlugin")
}

publishing {
  publications {
    shadow(MavenPublication) {publication ->
      from project.shadow.component(publication)
    }
  }
}
