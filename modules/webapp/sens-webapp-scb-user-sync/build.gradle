import com.github.jengelman.gradle.plugins.shadow.transformers.PropertiesFileTransformer

plugins {
  id "java"
  id "maven-publish"
  id 'com.github.johnrengelman.shadow' version '5.0.0'
}

description = "SENS WebApp User Sync"

configurations {
  implementation.extendsFrom starterDatabase
  runtimeOnly.extendsFrom starterDatabaseRuntime
  pluginJar
  compile.extendsFrom pluginJar
}

//If a new dependency is required then it has be added here and in the sens-webapp-backend/build.gradle
//or it could be included in built shadow JAR by using pluginJar configuration.
dependencies {
  implementation libraries.silenteight_sep_usermanagement_api
  implementation libraries.jakarta_validation_api

  implementation libraries.spring_boot_autoconfigure
  implementation libraries.spring_boot_starter_undertow

  implementation libraries.spring_security_web
  implementation libraries.spring_security_config
  implementation libraries.spring_security_cas

  implementation project(":sens-webapp-audit")
  implementation project(":sens-webapp-backend")
  implementation project(":sens-webapp-common")
  implementation project(":sens-webapp-logging")
  implementation project(":sens-webapp-user")

  implementation libraries.springdoc_security

  implementation libraries.oracle

  implementation libraries.vavr

  testImplementation libraries.rest_assured_spring_mock_mvc
}

shadowJar {
  archiveClassifier = 'plugin'

  mergeServiceFiles()

  append "META-INF/spring.handlers"
  append "META-INF/spring.schemas"

  configurations = [project.configurations.pluginJar]

  transform(PropertiesFileTransformer) {
    paths = ["META-INF/spring.factories"]
    mergeStrategy = "append"
  }
}

tasks.register("makePlugin", Copy).configure {
  from shadowJar
  into "$rootProject.projectDir/plugin/webapp"
}

tasks.register("deletePlugin", Delete).configure {
  delete fileTree("${rootProject.projectDir}/plugin/webapp") {
    include "$project.name-*.jar"
  }
}

tasks.named(JavaBasePlugin.BUILD_TASK_NAME).configure {
  dependsOn tasks.named("makePlugin")
}

tasks.named(BasePlugin.CLEAN_TASK_NAME).configure {
  dependsOn tasks.named("deletePlugin")
}

publishing {
  publications {
    shadow(MavenPublication) {publication ->
      from project.shadow.component(publication)
    }
  }
}
