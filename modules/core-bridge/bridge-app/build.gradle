plugins {
  id "idea"
  id "java"
  id "groovy"
  id "maven-publish"
  id "org.springframework.boot" version "2.5.7"
  id "com.google.cloud.tools.jib" version "3.2.1"
}

description = "bridge-app"

apply from: "${rootProject.projectDir}/gradle/scripts/dependencies.gradle"

jar {
  manifest {
    attributes "Main-Class": "com.silenteight.bridge.core.CoreBridgeApplication"
  }
}

jib {
  from {
    // NOTE(ahaczewski): Use debug version of the image for SNAPSHOT builds.
    def imageTag = "$version".endsWith("-SNAPSHOT") ? "debug-nonroot" : "nonroot"
    image = "gcr.io/distroless/java17-debian11:" + imageTag
  }
  to {
    image = "docker.repo.silenteight.com/cb/${project.name}:${version}"
    if ("$version".contains("-BUILD") || "$version".endsWith("-SNAPSHOT")) {
      tags = ["snapshot"]
    } else {
      tags = ["latest"]
    }
  }
  container {
    jvmFlags = [
        "-XX:MaxRAMPercentage=75",
        "-Dfile.encoding=UTF-8",
        "-Dsun.jnu.encoding=UTF-8",
        "-Djava.net.preferIPv4Stack=true",
        "-Djava.io.tmpdir=/tmp"
    ]
    user = "nonroot"
    workingDirectory = "/silenteight"
  }
}
