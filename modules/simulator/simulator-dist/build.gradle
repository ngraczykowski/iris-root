import org.apache.tools.ant.filters.ReplaceTokens

plugins {
  id "distribution"
  id "maven-publish"
}

description = "Simulator Distribution"

configurations {
  dist
}

dependencies {
  dist project(path: ":simulator-app", configuration: "bootArchives")
}

def runScript(String name) {
  copySpec {
    from(file("src/bash/run.template.sh")) {
      rename {
        name
      }
      filter(
          ReplaceTokens,
          tokens: [jarfile: "${name}-app-${project.version}-exec.jar".toString()],
          beginToken: "@@",
          endToken: "@@",
      )
      into("bin/")
    }
  }
}

distributions {
  main {
    distributionBaseName = "simulator-dist"

    contents {
      from(configurations.dist) {
        into "lib/"
      }
      with runScript("simulator")
    }
  }
}

tasks.named("distTar") {
  compression = Compression.BZIP2
  archiveExtension = "tar.bz2"
}

tasks.withType(Zip) {
  enabled = false
}

publishing {
  publications {
    distribution(MavenPublication) {
      artifact distTar
      artifactId "simulator-dist"
    }
  }
}
