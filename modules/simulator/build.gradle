plugins {
  alias(libs.plugins.jib) apply false
  alias(libs.plugins.freefair.aspectj.weaving) apply false
}

subprojects {Project subproject ->
  pluginManager.withPlugin("java") {
    subproject.pluginManager.apply "io.freefair.aspectj.post-compile-weaving"

    subproject.dependencies {
      implementation libs.jaxb.api
      annotationProcessor libs.spring.boot.configuration.processor
    }

    def ajcArgs = [
        "-Xlintfile", "${rootProject.projectDir}/Xlint.properties".toString(),
    ]

    subproject.tasks.withType(JavaCompile).configureEach {JavaCompile java ->
      java.ajc.options.compilerArgs = ajcArgs
      java.options.fork = true
      java.options.compilerArgs << "-parameters"
    }

    subproject.afterEvaluate {
      if (subproject.pluginManager.hasPlugin("com.google.cloud.tools.jib")) {
        subproject.jib {
          from {
            // NOTE(ahaczewski): Use debug version of the image for SNAPSHOT builds.
            def imageTag = "$version".endsWith("-SNAPSHOT") ? "debug-nonroot" : "nonroot"
            image = "gcr.io/distroless/java17-debian11:" + imageTag
          }
          to {
            image = "docker.repo.silenteight.com/simulator/${subproject.name}:${version}"
            if ("$version".contains("-BUILD") || "$version".endsWith("-SNAPSHOT")) {
              tags = ["snapshot"]
            } else {
              tags = ["latest"]
            }
          }
          container {
            appRoot = "/silenteight/simulator"
            environment = [
                "SILENTEIGHT_HOME"            : "/silenteight",
                "SIMULATOR_DB_HOST"           : "postgres",
                "SIMULATOR_DB_PORT"           : "5432",
                "SIMULATOR_DB_SCHEMA"         : "public",
                "SIMULATOR_DB_NAME"           : "simulator",
                "SIMULATOR_DB_USER"           : "simulator",
                "SIMULATOR_DB_PASSWORD"       : "simulator",
                "SPRING_RABBITMQ_HOST"        : "rabbitmq",
                "SPRING_RABBITMQ_PORT"        : "5672",
                "SPRING_RABBITMQ_USERNAME"    : "dev",
                "SPRING_RABBITMQ_PASSWORD"    : "dev",
                "SPRING_RABBITMQ_VIRTUAL_HOST": "/",
                "SPRING_PROFILES_ACTIVE"          : "linux,simulator,swagger,messaging,debug",
                "KEYCLOAK_ADAPTER_AUTH_SERVER_URL": "https://auth.silenteight.com/",
                "KEYCLOAK_CLIENT_ID": "backend",
                "KEYCLOAK_ADAPTER_RESOURCE":"backend",
                "KEYCLOAK_ADAPTER_CREDENTIALS_SECRET":"keycloak",
                "KEYCLOAK_ADAPTER_REALM":"sens-webapp"
            ]
            jvmFlags = [
                "-Xms1g",
                "-Xmx1g",
                "-Dfile.encoding=UTF-8",
                "-Dsun.jnu.encoding=UTF-8",
                "-Djava.net.preferIPv4Stack=true",
                "-Djava.io.tmpdir=/tmp",
            ]
            ports = ["24890"]
            user = "nonroot"
            workingDirectory = "/silenteight"
          }
        }
      }
    }
  }
}
