plugins {
  id "idea"
  id "com.silenteight.gradle.wrapper" version "1.1.0"
  id "com.silenteight.gradle.release" version "1.1.0"
  id "com.silenteight.gradle.convention" version "1.14.0"
}

ext {
  protobufVersion = "3.11.4"
  protocVersion = protobufVersion
  grpcVersion = "1.27.2"
  commonProtosVersion = "1.17.0"
  reactiveGrpcVersion = "1.0.0"
}

projectSetup {
  protobufGenerateGrpc = true
  protobufProtocVersion = project.protocVersion
  protobufGrpcVersion = project.grpcVersion

  protobufGenerateReactiveGrpc = true
  reactiveGrpcVersion = project.reactiveGrpcVersion
}

configure(subprojects) {Project subproject ->
  subproject.pluginManager.withPlugin("java-library") {
    subproject.jar {
      archiveBaseName = "se-${subproject.name}"
    }

    subproject.java {
      registerFeature('reactorSupport') {
        usingSourceSet(sourceSets.main)
        capability(project.group, "${rootProject.name}-reactor-support", "1.0")
      }
      registerFeature('rxSupport') {
        usingSourceSet(sourceSets.main)
        capability(project.group, "${rootProject.name}-rx-support", "1.0")
      }
    }

    subproject.dependencies {
      compileOnly "javax.annotation:javax.annotation-api:1.3.2"

      api "com.google.protobuf:protobuf-java:${protobufVersion}"
      api "com.google.api.grpc:proto-google-common-protos:${commonProtosVersion}"

      implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"

      compileOnly "io.grpc:grpc-stub:${grpcVersion}"
      compileOnly "io.grpc:grpc-protobuf:${grpcVersion}"
      compileOnly "com.google.api.grpc:grpc-google-common-protos:${commonProtosVersion}"

      reactorSupportApi "com.salesforce.servicelibs:reactor-grpc-stub:${reactiveGrpcVersion}"
      rxSupportApi "com.salesforce.servicelibs:rxgrpc-stub:${reactiveGrpcVersion}"

      testImplementation "io.grpc:grpc-testing:${grpcVersion}"
    }

    subproject.configurations {
      it.testImplementation.extendsFrom it.compileOnly
    }
  }
}
