plugins {
  id "org.sonarqube" version "3.0"
  id "java"
  id "idea"
  id "groovy"
  id "maven-publish"
  id "io.swagger.core.v3.swagger-gradle-plugin" version "2.1.6"
  id "org.springframework.boot" version '2.4.2'
  id "com.silenteight.gradle.release" version "1.4.0"
  id "com.google.cloud.tools.jib" version "3.2.1"
}

ext {
  gradleScriptDir = "${rootProject.projectDir}/gradle/scripts"
}

apply from: "${gradleScriptDir}/dependencies.gradle"

jar {
  manifest {
    attributes "Main-Class": "com.silenteight.hsbc.bridge.BridgeApplication"
  }
}

jib {
  from {
    // NOTE(ahaczewski): Use debug version of the image for SNAPSHOT builds.
    def imageTag = "$version".endsWith("-SNAPSHOT") ? "debug-nonroot" : "nonroot"
    image = "gcr.io/distroless/java11-debian11:" + imageTag
  }
  to {
    image = "docker.repo.silenteight.com/hsbc/${project.name}:${version}"
    if ("$version".contains("-BUILD") || "$version".endsWith("-SNAPSHOT")) {
      tags = ["snapshot"]
    } else {
      tags = ["latest"]
    }
  }
  container {
    appRoot = "/silenteight/app"
    jvmFlags = [
        "-XX:MaxRAMPercentage=75",
        "-Dfile.encoding=UTF-8",
        "-Dsun.jnu.encoding=UTF-8",
        "-Djava.net.preferIPv4Stack=true",
        "-Djava.io.tmpdir=/tmp",
        "-Dsilenteight.home=/silenteight"
    ]
    ports = ["8080", "9090"]
    user = "nonroot"
    workingDirectory = "/silenteight"
  }
}

if (project.hasProperty('unit')) {
  test.filter.excludeTestsMatching('*Integration*')
}

