plugins {
  id "maven-publish"
  id "org.sonarqube" version "3.0"
  id "com.silenteight.gradle.convention" version "1.21.4"
  id "com.silenteight.gradle.release" version "1.4.0"
  id "com.silenteight.gradle.wrapper" version "1.1.0"
  id "com.silenteight.gradle.testreportspublisher" version "1.2.0"
  id "nebula.resolution-rules" version "9.0.0"
  id "io.freefair.aspectj" version "5.3.0" apply false
  // Spring boot plugin version should be same as Spring version
  // in gradle/scripts/dependencies.gradle
  id "org.springframework.boot" version "2.6.6" apply false
  id "com.google.cloud.tools.jib" version "3.1.4" apply false
}

projectSetup {
  javaVersion = JavaVersion.VERSION_11
}

ext {
  gradleScriptDir = "${rootProject.projectDir}/gradle/scripts"
}

apply from: "${gradleScriptDir}/resolutionRules.gradle"

subprojects {Project subproject ->
  pluginManager.withPlugin("java") {
    archivesBaseName = "silenteight-" + subproject.name

    subproject.apply from: "${gradleScriptDir}/dependencies.gradle"
    subproject.pluginManager.apply "maven-publish"
    subproject.pluginManager.apply "io.freefair.aspectj.post-compile-weaving"

    subproject.aspectj {
      version = libraries.aspectjtools.version
    }

    subproject.tasks.named(JavaPlugin.COMPILE_JAVA_TASK_NAME).configure {JavaCompile java ->
      def processResources = project.tasks.named(JavaPlugin.PROCESS_RESOURCES_TASK_NAME)

      java.dependsOn processResources
    }

    project.tasks.withType(JavaCompile).configureEach { JavaCompile java ->
      java.options.fork = true
    }
  }
}
