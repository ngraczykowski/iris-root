plugins {
  id "java"
  id "com.google.cloud.tools.jib"
  id "org.springframework.boot" version "2.5.4"
}

dependencies {
  implementation platform(
      group: 'org.springframework.boot', name: 'spring-boot-dependencies', version: '2.5.4')

  implementation project(":sear-rabbitmq-common-schema-definitions")

  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-amqp'
}

bootJar {
  mainClass =
      "com.silenteight.rabbitcommonschema.initializr.app.RabbitCommonSchemaInitializrApplication"
}

jib {
  from {
    // NOTE(ahaczewski): Use debug version of the image for SNAPSHOT builds.
    def imageSuffix = ("$version".contains("-BUILD") || "$version".endsWith("-SNAPSHOT")) ?
                      "-debug" : ""
    image = "gcr.io/distroless/java-debian10:11" + imageSuffix
  }
  to {
    image = "docker.repo.silenteight.com/${project.name}:${version}"
    if ("$version".contains("-BUILD") || "$version".endsWith("-SNAPSHOT")) {
      tags = ["snapshot"]
    } else {
      tags = ["latest"]
    }
  }
  container {
    appRoot = "/silenteight/app"
    environment = [
        "SILENTEIGHT_HOME"            : "/silenteight",
        "SPRING_PROFILES_ACTIVE"      : "",
    ]
    jvmFlags = [
        "-XX:MaxRAMPercentage=75",
        "-Dfile.encoding=UTF-8",
        "-Dsun.jnu.encoding=UTF-8",
        "-Djava.net.preferIPv4Stack=true",
        "-Djava.io.tmpdir=/tmp",
        "-Dsilenteight.home=/silenteight",
        "-Dspring.profiles.include=container",
    ]
    user = "nonroot"
    workingDirectory = "/silenteight"
  }
}
